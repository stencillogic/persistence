==== Grammatics ====

[] - not mandatory
{} - zero or more times
() - grouping
<low_case> - nonterminal
other - terminals (optionally in double quotes)

=== BNF ===

<sql_statement> := <statement_body> ;

<statement_body> := <select_statement> |
                    <insert_statement> |
                    <update_statement> |
                    <delete_statement> |
                    <create_statement> |
                    <drop_statement> |
                    <alter_statement>

=== select ===

<select_statement> := <single_select_statement>
                      [ { ( UNION | INTERSECT | EXCEPT ) [ ALL ] <single_select_statement> } ]
                      [ ORDER BY <order_by_expr_list> ]

<order_by_expr_list> := <expression_with_sort> [ {, <expression_with_sort> } ]
<expression_with_sort> := <expression> [ ASC | DESC ] [ NULLS ( FIRST | LAST ) ]

<expression> := [ "+" | "-" ] <term_1> { <bin_op_1> <term_1> }
<term_1> := <term_2> { <bin_op_2> <term_2> }
<term_2> := <term_3> { <bin_op_3> <term_3> }
...
<term_8> := <literal> | <name> | NULL | "(" <expression> ")"

<bin_op_1> := OR
<bin_op_2> := AND
<bin_op_3> := NOT
<bin_op_4> := ( IS NULL ) | ( IS NOT NULL )
<bin_op_5> := "=" | "<>" | ">" | ">=" | "<" | "<="
<bin_op_6> := "+" | "-"
<bin_op_7> := "*" | "/"


<name> := <identifier> [ . <identifier> ]
<identifier> := _ | letter [ { _ | letter | digit } ]

<literal> := <string_literal> |
             <number_literal>

<string_literal> := ' { any_symbol_except_single_quote | '' } '

<number_literal> := { digit } [ . digit { digit } ] | digit { digit } [ . { digit } ]

<integer_literal> := digit { digit }

<single_select_statement> := SELECT [ ALL | DISTINCT ] <projection>
                             [ FROM <from> ]
                             [ WHERE <conditional_expr> ]
                             [ GROUP BY <expression_list> [ HAVING <predicate_list> ] ]
                             

<projection> := * | <named_expression_list>
<named_expression_list> := <named_expression> [ {, <named_expression> } ]
<named_expression> := <expression> [ ( AS <identifier> ) | <identifier> ]

<from> := <from_item> { [ ( [ INNER ] | [ CROSS ] | [ LEFT [ OUTER ] ] | [ RIGHT [ OUTER ] | [ FULL [ OUTER ] ] ) JOIN <from_item> [ ON <conditional_expr> ] ] }

<from_item> := <name> [ ( AS <identifier> ) | <identifier> ] |
              "(" <select_statement> ")" [ ( AS <identifier> ) | <identifier> ]

<conditional_expr> := <boolean_term> |
                      <conditional_expr> OR <boolean_term>

<boolean_term> := <boolean_factor> |
                  <boolean_term> AND <boolean_factor>

<boolean_factor> := [ NOT ] <boolean_test>
<boolean_test> := <predicate> | "(" <conditional_expr> ")"
<predicate> := <expression> ( > | < | >= | <= | = | <> ) <expression> | <expression> IS NULL

<expression_list> := <expression> [ {, <expression> } ]
<predicate_list> := <predicate> [ "," { <predicate> } ]

=== insert ===

<insert_statement> := INSERT INTO <name> [ <colname_list> ] ( VALUES "(" <expression>  [ { , <expression> } ] ")" | <select_statement> )
<colname_list> := "(" <identifier> [ { , <identifier> } ] ")"

=== update ===

<update_statement> := UPDATE <name> [ AS <identifier> ] SET <set_expression> [ { , <set_expression>  } ] [ WHERE <conditional_expr> ]

<set_expression> := <name> "=" <expression>

=== delete ===

<delete_statement> := DELETE [ FROM ] <name> [ [ AS ] <name> ] [ WHERE <conditional_expr> ]

=== create ===

<create_statement> := <create_table_statement> |
                      <crate_database_statement>

<create_table_statement> := CREATE TABLE <name> "(" <column_list> <table_constraints> ")"
<column_list> := <column_desc> { , <column_desc> } 
<column_desc> := <identifier> <data_type> [ DEFAULT <expression> ] [ [NOT] NULL ] 

<datatype> := <char_type_desc> | <numeric_type_desc> | <timestamp_type_desc> | SMALLINT | INTEGER | FLOAT | DOUBLE_PRECISION | DATE
<char_type_desc> := ( CHARACTER VARYING | VARCHAR ) "(" <integer_literal> ")"
<numeric_type_desc> := ( DECIMAL | NUMBER ) [ "(" <integer_literal> , <integer_literal>  ")" ]
<timestamp_type_desc> := TIMESTAMP [ "(" <integer_literal>  ")" ]


<table_constraint> := CONSTRAINT <identifier> (
                      CHECK "(" <expression> ")" | 
                      UNIQUE  <colname_list>  | 
                      PRIMARY KEY <colname_list>  |
                      FOREIGN KEY <colname_list>  
                        REFERENCES <name> [ <colname_list>  ] [ ON DELETE ( RESTRICT | CASCADE | SET NULL | SET DEFAULT | NO ACTION ) ] )

<create_database_statement> := CREATE DATABASE <identifier>

=== drop ===

<drop_statement> := <drop_table_statement> |
                    <drop_database_statement>

<drop_table_statement> := DROP TABLE <name>

<drop_database_statement> := DROP DATABASE <identifier>

=== alter ===

<alter_statement> := <alter_table_statement>

<alter_table_statement> := ALTER TABLE <name> ( <add_column> | <modify_column> | <drop_column> | <add_constraint> | <drop_constraint> | <rename> )
<add_column> := ADD [ COLUMN ] <identifier> <data_type> 
<modify_column> := ( ALTER | MODIFY ) [ COLUMN ] <identifier> [ <data_type> ]  [ DEFAULT <expression> ] [ [ NOT ] NULL ]
<drop column> := DROP [ COLUMN ] <identifier>
<add_constraint> := ADD <table_constraint>
<drop_constraint> := DROP CONSTRAINT <identifier>
<rename> := RENAME [ ( COLUMN | CONSTRAINT ) <identifier> ] TO <identifier>


